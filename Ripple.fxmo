// Ripple

//@Begin_vertex
//version_####
#ifdef GL_ES
 precision mediump float;
#endif
in vec2 position;

uniform mat3 projectionMatrix;
uniform mat3 transformMatrix;
uniform mat3 objectMatrix;
uniform mat3 textureMatrix;

out vec2 textureCoordinate;

void main()
{
    vec3 pos = vec3(position, 1.0);
    textureCoordinate = (textureMatrix * pos).xy;
    gl_Position = vec4(projectionMatrix * transformMatrix * objectMatrix * pos, 1.0);
}
//@End
//@Begin_fragment
//version_####
#ifdef GL_ES
 precision mediump float;
#endif
in vec2 textureCoordinate;

uniform float freq;
uniform float speed;
uniform float amplitude;
uniform float centerx;
uniform float centery;
uniform float effect;

uniform sampler2D imgTexture;
uniform lowp vec4 blendColor;

out vec4 fragColor;

void main(void)
{
    vec2 center = vec2(centerx, centery);
    vec2 toUV = textureCoordinate - center;
    float distanceFromCenter = length(toUV);
    vec2 normToUV = toUV / max(distanceFromCenter, 0.0001);

    float wave = cos(freq * distanceFromCenter - speed * effect);
    float offset1 = effect * wave * amplitude;
    float offset2 = (1.0 - effect) * wave * amplitude;

    vec2 newUV1 = center + normToUV * (distanceFromCenter + offset1);
    vec2 newUV2 = center + normToUV * (distanceFromCenter + offset2);

    vec4 c1 = texture(imgTexture, newUV1);
    vec4 c2 = texture(imgTexture, newUV2);

    vec4 color = mix(c1, c2, effect) * blendColor;
    fragColor = color;
}
//@End
