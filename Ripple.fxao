// Ripple

//@Begin_vertex
#ifdef GL_ES
 precision mediump float;
#endif
uniform mat4 transformMatrix;
uniform mat4 projectionMatrix;

attribute vec4 position;
attribute vec2 texCoord;
varying vec2 textureCoordinate;

uniform float flipm;

void main(void)
{
    textureCoordinate = mix(texCoord, vec2(texCoord.x, 1.0 - texCoord.y), flipm);
    gl_Position = projectionMatrix * transformMatrix * position;
}
//@End
//@Begin_fragment
#ifdef GL_ES
 precision mediump float;
#endif
varying vec2 textureCoordinate;
uniform float freq;
uniform float speed;
uniform float amplitude;
uniform float centerx;
uniform float centery;
uniform float effect;
uniform sampler2D imgTexture;
uniform sampler2D bckgTexture;
void main(void)
{
    vec2 center = vec2(centerx, centery);
    vec2 toUV = textureCoordinate - center;
    float distanceFromCenter = length(toUV);
    vec2 normToUV = toUV / max(distanceFromCenter, 0.0001);

    float wave = cos(freq * distanceFromCenter - speed * effect);
    float offset1 = effect * wave * amplitude;
    float offset2 = (1.0 - effect) * wave * amplitude;
    
    vec2 newUV1 = center + normToUV * (distanceFromCenter + offset1);
    vec2 newUV2 = center + normToUV * (distanceFromCenter + offset2);

    vec4 c1 = texture2D(bckgTexture, newUV1);
    vec4 c2 = texture2D(bckgTexture, newUV2);

    vec4 color = mix(c1, c2, effect);
    color.a = 1.0;
    
    gl_FragColor = color;
}
//@End